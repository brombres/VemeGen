class IMFont( name:String, point_size:Int32 )
  METHODS
    method create_subtitled_frame( frame_size:XY, subtitle:String, outfile:File )
      local line_height = measure( subtitle ).y
      local subtitle_height = measure( subtitle ).y
      local origin_y = frame_size.y/2 - (line_height + (subtitle_height/2))

      local cmd = @|magick -size $FRAME_SIZE canvas:none -font $FONT -pointsize $POINT_SIZE -gravity center \
                   |  -draw "text $SHADOW_ORIGIN \'$TEXT\'" -channel RGBA -blur 6x8 -fill black -stroke black \
                   |  -fill white -stroke white -draw "text $ORIGIN \'$TEXT\'" $FILEPATH
      cmd .= replacing( "$FILEPATH",      outfile.esc )
      cmd .= replacing( "$FONT",          this.name )
      cmd .= replacing( "$FRAME_SIZE",    "$x$"(frame_size.x,frame_size.y) )
      cmd .= replacing( "$ORIGIN",        "0,$"(origin_y) )
      cmd .= replacing( "$POINT_SIZE",    this.point_size->String )
      cmd .= replacing( "$SHADOW_ORIGIN", "0,$"(origin_y+point_size/9) )
      cmd .= replacing( "$TEXT",          subtitle.replacing("\"", "\\\"").replacing("'","\\\\\\'") )
      local result = Process.run( cmd, &env )
      if (not result.success)
        throw Error( ''[Error] Failed to create subtitle frame "$".''(subtitle) )
      endIf

    method measure( text:String )->XY
      text .= replacing( "\"", "\\\"" )
      local cmd = ''magick -font $ -pointsize $ -verbose label:"$" png:- > /dev/null''(name,point_size,text)
      local result = Process.run( cmd, &env )
      if (result.success)
        local dims = result->String.after_first("LABEL ").before_first(' ').split('x')
        if (dims.count == 2)
          return XY( dims[0]->Int32, dims[1]->Int32 )
        endIf
      endIf

      throw Error( "[Error] Failed to determine text size using ImageMagick: $"(result) )
endClass
