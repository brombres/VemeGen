class Script
  PROPERTIES
    filepath  : String
    subtitles = String[]
    type      : String

  METHODS
    method init( file:File, type_option:String )
      println "Reading script '$'"(file)
      forEach (line in LineReader(file))
        line .= trimmed

        if (line == "" or line.begins_with('#')) nextIteration

        if (line.begins_with("type "))
          type = line.after_first(' ').trimmed
        elseIf (line.contains( ':' ))
          subtitles.add( line.after_first(':') )
        else
          subtitles.add( line )
        endIf
      endForEach

      if (type_option and type and type_option != type)
        throw Error( "[Error] Veme type mismatch; command line specifies '--type=$' but script specifies 'type $'."(type_option,type) )
      endIf

      if (not type) type = type_option
      if (not type) type = "HitlerReacts"

    method count->Int32
      return subtitles.count

    method get( index:Int32 )->String
      return subtitles[index]

    method to->String
      local result = String()
      result.println (forEach in subtitles)
      return result

endClass
