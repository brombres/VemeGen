class VGFont
  PROPERTIES
    font : BitmapFont

  METHODS
    method init( font_name:String, pixel_height:Int32 )
      font = BitmapFont( font_name, pixel_height )

    method create_subtitled_frame( frame_size:XY, subtitle:String, outfile:File )
      subtitle = word_wrap( subtitle, frame_size.x * 0.9, &even )
      local subtitle_size = font.measure( subtitle )

      local frame = Bitmap( frame_size )
      local origin = Box( frame_size-XY(0,font.height) ).at(0.5,1.0)
      forEach (shadow_dy in 1..(font.height/10).or_larger(2))
        local shadow_offset = XY( shadow_dy/2, shadow_dy )
        font.draw( subtitle, frame, origin+shadow_offset, Anchor.BOTTOM, AlphaFillBitmapBlitFn(Color.BLACK) )
      endForEach
      font.draw( subtitle, frame, origin, Anchor.BOTTOM )
      outfile.save( frame.to_png_bytes )

    method measure( text:String, i1=0:Int32, i2=null:Int32? )->XY
      i1 .= clamped( 0, text.count-1 )

      if (i2) i2 = i2.value.clamped( 0, text.count - 1)
      else    i2 = text.count - 1

      if (i1 > i2.value) return XY(0,0)

      use buffer = StringPool
        buffer.reserve( (i2.value-i1)+1 )
        forEach (i in i1..i2.value)
          buffer.print( text[i] )
        endForEach

        return font.measure( buffer )
      endUse

    method word_wrap( text:String, pixel_width:Int32, buffer=null:String, &even )->String
      # Prints a word-wrapped version of the given string into an optional
      # String buffer. Existing newline characters will cause a new line to
      # begin immediately. Spaces immediately following existing newline
      # characters are preserved.
      #
      # If &even is specified, the wrap width is dynamically reduced so that all
      # lines are approximately the same size (an "even" width).
      if (even)
        use builder = StringPool
          local line_count = word_wrap( text, pixel_width, builder ).count( '\n' ) + 1
          pixel_width = word_wrap_find_even_width( text, pixel_width/2, pixel_width, line_count )
        endUse
      endIf

      ensure<<buffer>>( (text.count * 1.1)->Int32 )

      local i1, i2 : Int32
      local len = text.count
      local w = 0.0

      if (text == "" or pixel_width <= 0) return buffer

      local needs_newline = false
      local starting_i2 = i2
      while (i2 < len)
        starting_i2 = i2

        # find last space or first \n
        while (w <= pixel_width and i2 < len and text[i2] != '\n')
          w = measure( text, i1, i2 ).x
          i2++
        endWhile

        local initial_i2 = i2

        if (w > pixel_width)
          --i2
          while (text[i2]!=' ' and text[i2]!='\n' and i2>i1) i2--
          if (i2 == i1) i2 = initial_i2 - 1
        endIf

        if (needs_newline) buffer.println
        forEach (i in i1..<i2) buffer.print( text[i] )
        needs_newline = true
        w = 0

        if (i2 == len)
          return buffer
        else
          which (text[i2])
            case ' ':
              while (i2<len and text[i2]==' ') i2++
              if (i2<len and text[i2]=='\n') i2++

            case '\n':
              i2++
          endWhich
          i1 = i2
        endIf

        if (starting_i2 == i2) return buffer  # Kludge/error

      endWhile

      return buffer

    method word_wrap_find_even_width( text:String, min:Int32, max:Int32, line_count:Int32 )->Int32
      local mid = (min + max) / 2
      if (mid == min) return max

      use builder = StringPool
        local mid_line_count = word_wrap( text, mid, builder ).count('\n') + 1

        if (mid_line_count > line_count)
          return word_wrap_find_even_width( text, mid, max, line_count )
        else
          return word_wrap_find_even_width( text, min, mid, line_count )
        endIf
      endUse
endClass
